config {
  type: "table",
  schema: "DWHDF",
  name: "TLDB1_VENDITE_RESI_1",
  tags: ["scontrini"],
  description: ""
}

WITH VENDITE_RESI AS (
  SELECT
    RESI.DWB1_ID_SCN_COD,
    RESI.DWB1_ID_RGH_COD,
    VENDITE.DWB1_ID_SCN_COD AS DWB1_ID_SCN_COLLEGATO_COD,
    VENDITE.DWB1_ID_RGH_COD AS DWB1_ID_RGH_COLLEGATO_COD,
    1 AS DWB1_CICLO_COLLEGATO_NUM,
    DATE_DIFF(
      RESI.DWB1_EMISSIONE_DTA,
      VENDITE.DWB1_EMISSIONE_DTA,
      DAY
    ) AS DWB1_SCN_COLLEGATO_GG_DLTA_NUM,
    ROW_NUMBER() OVER (
      PARTITION BY RESI.DWB1_ID_SCN_COD,
      RESI.DWB1_ID_RGH_COD
      ORDER BY
        RESI.DWB1_EMISSIONE_DTA ASC,
        VENDITE.DWB1_EMISSIONE_DTA DESC
    ) as RESI_ROW_NUMBER,
    ROW_NUMBER() OVER (
      PARTITION BY VENDITE.DWB1_ID_SCN_COD,
      VENDITE.DWB1_ID_RGH_COD
      ORDER BY
        RESI.DWB1_EMISSIONE_DTA ASC,
        VENDITE.DWB1_EMISSIONE_DTA DESC
    ) as VENDITE_ROW_NUMBER,
    CASE
      WHEN (
        SUM(ABS(VALORE_VENDITA) - ABS(VALORE_RESO)) OVER(
          PARTITION BY RESI.DWB1_ID_SCN_COD,
          RESI.DWB1_ID_RGH_COD
      ORDER BY
        RESI.DWB1_EMISSIONE_DTA ASC,
        VENDITE.DWB1_EMISSIONE_DTA DESC
        )
      ) = 0 THEN 0
      ELSE 1
    END AS SOMMA_ZERO
  FROM
    ${ref("TLDB1_VENDITE_1")} AS VENDITE
    INNER JOIN ${ref("TLDB1_RESI_1")} AS RESI ON VENDITE.DWG1_CLIE_DWH_COD = RESI.DWG1_CLIE_DWH_COD
    AND VENDITE.DWD4_ARTICOLO_COD = RESI.DWD4_ARTICOLO_COD
    AND VENDITE.DWM9_COLORE_COD = RESI.DWM9_COLORE_COD
    AND VENDITE.DWMH_TAGLIA_COD = RESI.DWMH_TAGLIA_COD
    AND VENDITE.DWD1_NEGZ_COD = RESI.DWD1_NEGZ_COD
    AND VENDITE.DWB1_EMISSIONE_DTA < RESI.DWB1_EMISSIONE_DTA
    AND VENDITE.DWB1_EMISSIONE_DTA >= DATE_ADD(
      RESI.DWB1_EMISSIONE_DTA,
      INTERVAL -12 * RESI.MOLTIPLICATORE_GG MONTH
    )
)
SELECT
  DWB1_ID_SCN_COD,
  DWB1_ID_RGH_COD,
  DWB1_ID_SCN_COLLEGATO_COD,
  DWB1_ID_RGH_COLLEGATO_COD,
  DWB1_CICLO_COLLEGATO_NUM,
  DWB1_SCN_COLLEGATO_GG_DLTA_NUM
FROM
  VENDITE_RESI
WHERE
  RESI_ROW_NUMBER = 1
  and VENDITE_ROW_NUMBER = 1
